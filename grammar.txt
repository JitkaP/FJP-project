program = block "."

block = [ "const" "int" ident ":=" number { "," ident ":=" number } ";" ]
		[ "const" "bool" ident ":=" boolean { "," ident ":=" boolean } ";" ]
        [ "var" "int" ident { , ident } ";" ]	
		[ "var" "bool" ident { , ident } ";" ]	
        { "procedure" ident ";" block ";" } 
		statement

statement = [
				ident { ":=" ident } ":=" expression 
				| "call" ident 
				| "?" ident 
				| "!" expression 
				| "begin" statement { statement } "end"
				| "if" condition "then" statement [ "else" statement ]
				| "while" condition "do" statement
				| "do" statement "while" condition ";"
				| "for" ident ":=" number_expression "to" number_expression "do" statement
				| "{" ident { "," ident } "}" ":="
					"{" expression { "," expression } "}" 
					":=" "{" expression { "," expression } "}"
				| ident ":=" condition "?" expression ":" expression
			] ";"

condition = "(" 
				( "odd" number_expression
					| number_expression ( "<" | "<=" | ">" | ">=" ) number_expression 
					| expression ( "=" | "!=" ) expression )	
			")"

expression = number_expression | boolean

number_expression = [ "+" | "-" ] term { ( "+" | "-" ) term }

term = factor { ( "*" | "/" ) factor }

factor = ident 
			| number 
			| "(" number_expression ")"
			
ident = a..z
number = 0..9
boolean = "true" | "false"